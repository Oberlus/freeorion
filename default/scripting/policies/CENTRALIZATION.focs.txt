Policy
    name = "PLC_CENTRALIZATION"
    description = "PLC_CENTRALIZATION_DESC"
    short_description = "PLC_CENTRALIZATION_SHORT_DESC"
    category = "ECONOMIC_CATEGORY"
    adoptioncost = 1.0 * floor(5 + 0.2 * Statistic Sum value = LocalCandidate.Population
                                          condition = And [ Planet OwnedBy empire = Source.Owner ])
    exclusions = [ "PLC_CONFEDERATION" "PLC_COLONIZATION" "PLC_ISOLATION" ]
    unlock = Item type = Building name = "BLD_REGIONAL_ADMIN"
    effectsgroups = [
        [[SPECIES_LIKES_OR_DISLIKES_POLICY_STABILITY_EFFECTS]]

        EffectsGroup
            scope = And [
                OwnedBy empire = Source.Owner
                Happiness low = 15
                Or [
                    Capital
                    Contains Building name = "BLD_REGIONAL_ADMIN"
                ]
            ]
            effects = [
                SetTargetIndustry value = Value + Target.Population
                    * (NamedReal name = "PLC_CENTRALIZATION_TARGET_INDUSTRY_PERPOP" value = 0.5 * [[INDUSTRY_PER_POP]])
                SetTargetResearch value = Value + Target.Population
                    * (NamedReal name = "PLC_CENTRALIZATION_TARGET_RESEARCH_PERPOP" value = 0.5 * [[RESEARCH_PER_POP]])
                SetTargetConstruction value = Value
                    + (NamedReal name = "PLC_CENTRALIZATION_TARGET_CONSTRUCTION_FLAT" value = 10)
                SetMaxStockpile value = Value
                    + (NamedReal name = "PLC_CENTRALIZATION_MAX_STOCKPILE_FLAT" value = -2)
            ]

        EffectsGroup
            scope = And [
                OwnedBy empire = Source.Owner
                Not Capital
                Not Contains Building name = "BLD_REGIONAL_ADMIN"
            ]
            effects = SetMaxSupply value = Value + (NamedReal name = "PLC_CENTRALIZATION_MAX_SUPPLY_FLAT" value = -1)

        EffectsGroup
            scope = And [
                Planet
                OwnedBy empire = Source.Owner
                Population low = 0.001
                Not ResourceSupplyConnected empire = LocalCandidate.Owner condition = And [
                    Planet
                    OwnedBy empire = Source.Owner
                    Capital
                ]
            ]
            effects = SetTargetInfluence value = Value + (NamedReal name = "PLC_CENTRALIZATION_TARGET_INFLUENCE_FLAT" value = -3)

        EffectsGroup
            scope = And [
                Planet
                Not Capital
                OwnedBy empire = Source.Owner
                Population low = 0.001
                ResourceSupplyConnected empire = LocalCandidate.Owner condition = And [
                    Planet
                    OwnedBy empire = Source.Owner
                    Capital
                ]
            ]
            effects = SetTargetInfluence value = Value + max(
                        (NamedReal name = "PLC_CENTRALIZATION_TARGET_INFLUENCE_MAX" value = -3),
                        (NamedReal name = "PLC_CENTRALIZATION_TARGET_INFLUENCE_PERJUMP" value = -0.2)
                            * JumpsBetween object = Source.ID object = Target.ID)

        EffectsGroup    // overrides disconnection penalty in happiness.macros with a greater penalty
            scope = And [
                Planet
                Not Capital
                OwnedBy empire = Source.Owner
                Population low = 0.001
                Not ResourceSupplyConnected empire = LocalCandidate.Owner condition = And [
                    Planet
                    OwnedBy empire = Source.Owner
                    Capital
                ]
            ]
            accountinglabel = "CENTRALIZATION_DISCONNECTION_LABEL"
            effects = SetTargetHappiness value = Value - (NamedReal name = "DISCONNECTED_CENTRALIZATION_STABILITY_PENALTY" value = 10)
    ]
    graphic = "icons/policies/centralization.png"

#include "/scripting/common/base_prod.macros"
#include "/scripting/policies/policies.macros"
